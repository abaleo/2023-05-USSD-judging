vagrant

high

# An attacker can make the USSDRebalancer sell more USSD than intended

## Summary
An attacker can make the USSDRebalancer sell more USSD than intended by calling USSD#mintRebalancer() before rebalance() is executed

## Vulnerability Detail
USSDRebalancer#rebalance() calls SellUSSDBuyCollateral() if 
```solidity 
(ownval > 1e6 + threshold)
```

```solidity
    function rebalance() override public {
      uint256 ownval = getOwnValuation();
      (uint256 USSDamount, uint256 DAIamount) = getSupplyProportion();
      if (ownval < 1e6 - threshold) {
        // peg-down recovery
        BuyUSSDSellCollateral((USSDamount - DAIamount / 1e12)/2);
      } else if (ownval > 1e6 + threshold) {
        // mint and buy collateral
        // never sell too much USSD for DAI so it 'overshoots' (becomes more in quantity than DAI on the pool)
        // otherwise could be arbitraged through mint/redeem
        // the execution difference due to fee should be taken into accounting too
        // take 1% safety margin (estimated as 2 x 0.5% fee)
        IUSSD(USSD).mintRebalancer(((DAIamount / 1e12 - USSDamount)/2) * 99 / 100); // mint ourselves amount till balance recover
        SellUSSDBuyCollateral();
      }
    }
```

SellUSSDBuyCollateral() sells the whole USSD balance of the USSD which is assumed to be the minted amount in rebalance() before the call to SellUSSDBuyCollateral:

```solidity
    function SellUSSDBuyCollateral() internal {
      uint256 amount = IUSSD(USSD).balanceOf(USSD); // @audit what if the balance of USSD contract is increased via mintRebalancer()
      // sell for DAI then swap by DAI routes
      uint256 daibought = 0;
      if (uniPool.token0() == USSD) {
        daibought = IERC20Upgradeable(baseAsset).balanceOf(USSD);
        IUSSD(USSD).UniV3SwapInput(bytes.concat(abi.encodePacked(uniPool.token0(), hex"0001f4", uniPool.token1())), amount);
        daibought = IERC20Upgradeable(baseAsset).balanceOf(USSD) - daibought; // would revert if not bought
      } else {
        daibought = IERC20Upgradeable(baseAsset).balanceOf(USSD);
        IUSSD(USSD).UniV3SwapInput(bytes.concat(abi.encodePacked(uniPool.token1(), hex"0001f4", uniPool.token0())), amount);
        daibought = IERC20Upgradeable(baseAsset).balanceOf(USSD) - daibought; // would revert if not bought
      }

```
Yet there is no restriction on who can call USSD#mintRebalancer().


```solidity
    function mintRebalancer(uint256 amount) public override { // @audit anyone can call this function
        _mint(address(this), amount);
    }
```
Anyone could call mintRebalancer() before the rebalance() call and artificially increase USSD balance in the USSD contract which will all be sold in SellUSSDBuyCollateral() as UniV3SwapInput() is called with _sellAmount as the whole USSD balance of the USSD contract:

```solidity
    function UniV3SwapInput(
        bytes memory _path,
        uint256 _sellAmount
    ) public override onlyBalancer {
        IV3SwapRouter.ExactInputParams memory params = IV3SwapRouter
            .ExactInputParams({
                path: _path,
                recipient: address(this),
                //deadline: block.timestamp,
                amountIn: _sellAmount,
                amountOutMinimum: 0 // @audit this is ????????????? isnt this dangerous? amount out minimum should never be 0, right???
            });
        uniRouter.exactInput(params);
    }
```


## Impact

Unrestricted access to important function breaks the whole protocol.

## Code Snippet
https://github.com/sherlock-audit/2023-05-USSD/blob/main/ussd-contracts/contracts/USSD.sol#L204
https://github.com/sherlock-audit/2023-05-USSD/blob/main/ussd-contracts/contracts/USSDRebalancer.sol#L164


## Tool used

Manual Review

## Recommendation
restrict access to mintRebalancer() 
