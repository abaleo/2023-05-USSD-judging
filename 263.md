Dug

medium

# At certain DAI collateral balances `BuyUSSDSellCollateral` will revert

## Summary

In the `BuyUSSDSellCollateral` function of `USSDRebalancer`, when the value of underlying DAI collateral is less than `amountToBuyLeftUSD` but greater than the minimum threshold of `amountToBuyLeftUSD / 20`, the function will revert.

## Vulnerability Detail

The `BuyUSSDSellCollateral` function is defined as follows...

```solidity
    function BuyUSSDSellCollateral(uint256 amountToBuy) internal {
        CollateralInfo[] memory collateral = IUSSD(USSD).collateralList();
        //uint amountToBuyLeftUSD = amountToBuy * 1e12 * 1e6 / getOwnValuation();
        uint256 amountToBuyLeftUSD = amountToBuy * 1e12;
        uint256 DAItosell = 0;
        // Sell collateral in order of collateral array
        for (uint256 i = 0; i < collateral.length; i++) {
            uint256 collateralval = IERC20Upgradeable(collateral[i].token).balanceOf(USSD) * 1e18
                / (10 ** IERC20MetadataUpgradeable(collateral[i].token).decimals()) * collateral[i].oracle.getPriceUSD()
                / 1e18;

            if (collateralval > amountToBuyLeftUSD) {
                // sell a portion of collateral and exit
                if (collateral[i].pathsell.length > 0) {
                    uint256 amountBefore = IERC20Upgradeable(baseAsset).balanceOf(USSD);
                    uint256 amountToSellUnits = IERC20Upgradeable(collateral[i].token).balanceOf(USSD)
                        * ((amountToBuyLeftUSD * 1e18 / collateralval) / 1e18) / 1e18;
                    IUSSD(USSD).UniV3SwapInput(collateral[i].pathsell, amountToSellUnits);
                    amountToBuyLeftUSD -= (IERC20Upgradeable(baseAsset).balanceOf(USSD) - amountBefore);
                    DAItosell += (IERC20Upgradeable(baseAsset).balanceOf(USSD) - amountBefore);
                } else {
                    // no need to swap DAI
                    DAItosell =
                        IERC20Upgradeable(collateral[i].token).balanceOf(USSD) * amountToBuyLeftUSD / collateralval;
                }
                break;
            } else {
                // sell all or skip (if collateral is too little, 5% treshold)
                if (collateralval >= amountToBuyLeftUSD / 20) {
                    uint256 amountBefore = IERC20Upgradeable(baseAsset).balanceOf(USSD);
                    // sell all collateral and move to next one
                    IUSSD(USSD).UniV3SwapInput(
                        collateral[i].pathsell, IERC20Upgradeable(collateral[i].token).balanceOf(USSD)
                    );
                    amountToBuyLeftUSD -= (IERC20Upgradeable(baseAsset).balanceOf(USSD) - amountBefore);
                    DAItosell += (IERC20Upgradeable(baseAsset).balanceOf(USSD) - amountBefore);
                }
            }
        }
    }
```

It steps through each collateral type and makes the swaps necessary to fulfill the `amountToBuyLeftUSD` amount. 

It is important to understand how the logic detects and handles DAI collateral specifically. It does this by checking the `pathsell` property of the collateral in question. If `pathsell.length == 0`, then the collateral is DAI. If `pathsell.length > 0`, then the collateral is not DAI.

DAI does not need to be swapped, so the logic should skip the call to UniV3SwapInput. This is done correctly inf the if case where `collateralval > amountToBuyLeftUSD`, however, it is not done correctly in the else case where `collateralval >= amountToBuyLeftUSD / 20`.

If the amount of DAI collateral is valued at less than `amountToBuyLeftUSD` but greater than `amountToBuyLeftUSD / 20`, then a call is made to UniV3SwapInput with an empty path. This will cause the transaction to revert.

## Impact

This means that at certain times when the value of DAI falls between `amountToBuyLeftUSD` and `amountToBuyLeftUSD / 20`, the `BuyUSSDSellCollateral` function will revert and the rebalancer will be unable to keep USSD pegged to the dollar.

## Code Snippet

https://github.com/sherlock-audit/2023-05-USSD/blob/main/ussd-contracts/contracts/USSDRebalancer.sol#L109-L140

## Tool used

Manual Review

## Recommendation

Do not attempt to swap the DAI collateral if the value is less than `amountToBuyLeftUSD` but greater than `amountToBuyLeftUSD / 20`. 
