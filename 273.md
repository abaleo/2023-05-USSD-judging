Dug

high

# Underlying collateral can be drained from the protocol

## Summary

An attacker can utilize the public `USSD.mintRebalancer()` function to create large token imbalances, allowing them to drain backing collateral from the system.

## Vulnerability Detail

To illustrate this scenario, we will consider the case where the USSD/DAI Uniswap pool has 1000 of each tokens. These are the balances that the whitepaper says the pool will be initiated with.

To start this attack, a malicious user first needs to purchase enough USSD tokens from the pool to enable a `rebalance()` call where `ownval > 1e6 + threshold`. 

Let's say the threshold is `10 * 1e4`, representing 10 cents of slippage. This means that the attacker could swap for 50 USSD, making the pool balances 950 USSD and ~1053 DAI. Rebalancing is then enabled as it will calculate the value of USSD to be just over `1.1 * 1e6`.

However before rebalancing, the attacker calls `USSD.mintRebalancer()` passing in an fairly large value (intended to be comparable to the pool balance of DAI) `900 * 1e6` as the `amount` parameter. This will cause 900 USSD tokens to be minted and held within the USSD contract.

Then the attacker calls `rebalance()`...

```solidity
    function rebalance() public override {
        uint256 ownval = getOwnValuation();
        (uint256 USSDamount, uint256 DAIamount) = getSupplyProportion();
        if (ownval < 1e6 - threshold) {
            // peg-down recovery
            BuyUSSDSellCollateral((USSDamount - DAIamount / 1e12) / 2);
        } else if (ownval > 1e6 + threshold) {
            // mint and buy collateral
            // never sell too much USSD for DAI so it 'overshoots' (becomes more in quantity than DAI on the pool)
            // otherwise could be arbitraged through mint/redeem
            // the execution difference due to fee should be taken into accounting too
            // take 1% safety margin (estimated as 2 x 0.5% fee)
            IUSSD(USSD).mintRebalancer(((DAIamount / 1e12 - USSDamount) / 2) * 99 / 100); // mint ourselves amount till balance recover
            SellUSSDBuyCollateral();
        }
    }
```

As part of that execution an additional 25 USSD tokens are minted and held within the USSD contract. 

Control is then passed to `SellUSSDBuyCollateral()` where the first expression retrieves the USSD token balance from the USSD contract, which is 925. This amount is used to swap with DAI, moving ~520 DAI into the USSD contract as collateral.

```solidity
    function SellUSSDBuyCollateral() internal {
        uint256 amount = IUSSD(USSD).balanceOf(USSD);
        // sell for DAI then swap by DAI routes
        uint256 daibought = 0;
        if (uniPool.token0() == USSD) {
            daibought = IERC20Upgradeable(baseAsset).balanceOf(USSD);
            IUSSD(USSD).UniV3SwapInput(
                bytes.concat(abi.encodePacked(uniPool.token0(), hex"0001f4", uniPool.token1())), amount
            );
            daibought = IERC20Upgradeable(baseAsset).balanceOf(USSD) - daibought; // would revert if not bought
        } else {
            daibought = IERC20Upgradeable(baseAsset).balanceOf(USSD);
            IUSSD(USSD).UniV3SwapInput(
                bytes.concat(abi.encodePacked(uniPool.token1(), hex"0001f4", uniPool.token0())), amount
            );
            daibought = IERC20Upgradeable(baseAsset).balanceOf(USSD) - daibought; // would revert if not bought
        }
        ...

```     

The pool balance is now at 1875 USSD and ~533 DAI. With these balances, the attacker can swap DAI for USSD at a huge discount, effectively resetting the peg. The attacker would then have 875 USSD and the pool would be back to 1000 of each token.

So, at this point the attacker now has an excess of USSD, and the protocol has ~520 DAI as collateral. The attacker can then proceed to incrementally swap their USSD for DAI, calling `rebalance()` as needed to keep the value pegged around $1. They are able to then drain the ~520 DAI of collateral from the system as they swap their stolen USSD for it.

When the ~520 in DAI collateral is drained, the protocol is left insolvent as there is still unbacked USSD in circulation.

## Impact

An attacker is able to steal a large amount of value from the system. While a newly initialized pool was used in the example above, the same attack could work for much larger amounts should the pool have higher liquidity.

## Code Snippet

https://github.com/sherlock-audit/2023-05-USSD/blob/main/ussd-contracts/contracts/USSD.sol#L204-L206

https://github.com/sherlock-audit/2023-05-USSD/blob/main/ussd-contracts/contracts/USSDRebalancer.sol#L92-L107

https://github.com/sherlock-audit/2023-05-USSD/blob/main/ussd-contracts/contracts/USSDRebalancer.sol#L163-L205

## Tool used

Manual Review

## Recommendation

Add access control to the mint function, allowing only the rebalancer to mint USSD.

```diff
-   function mintRebalancer(uint256 amount) public override {
+   function mintRebalancer(uint256 amount) public override onlyBalancer {
        _mint(address(this), amount);
    }
```

Alternatively, pass the amount to sell into `SellUSSDBuyCollateral` instead of reading it as the total USSD balance of the USSD contract.