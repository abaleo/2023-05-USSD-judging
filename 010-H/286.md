Schpiel

medium

# rebalance() is using spot price in Uniswap liquidity pool, which is very vulnerable to flashloan price manipulation

## Summary
In `USSDRebalancer`, anyone is able to call the `rebalance` operation. When calculating if buying/selling of collateral is required, `getOwnValuation()` is called, which calculates the price estimation to DAI using the spot price. This is very susceptible to a flashloan price manipulation. A hacker can force the protocol to rebalance their collateral anytime, resulting in the protocol purchasing more collateral, even when it doesn't need to. 
## Vulnerability Detail
When `rebalance` is called, in order to determine if collateral needs to be purchased or sold, `getOwnValuation` and `getSupplyProportion` are called.
```solidity
    function rebalance() public override {
        uint256 ownval = getOwnValuation();
        (uint256 USSDamount, uint256 DAIamount) = getSupplyProportion();
```

Below is the code for `getOwnValuation`, which is using the spot price to calculate the current price to DAI. This price is used to determine if the threshold has been met to buy/sell collateral.
```solidity
    function getOwnValuation() public view returns (uint256 price) {
        (uint160 sqrtPriceX96, , , , , , ) = uniPool.slot0();
        if (uniPool.token0() == USSD) {
            price =
                ((uint(sqrtPriceX96) * (uint(sqrtPriceX96))) / (1e6)) >>
                (96 * 2);
        } else {
            price =
                (uint(sqrtPriceX96) *
                    (uint(sqrtPriceX96)) *
                    (1e18)) /* 1e12 + 1e6 decimal representation */ >>
                (96 * 2);
            // flip the fraction
            price = (1e24 / price) / 1e12;
        }
    }
``` 

Additionally, `getSupplyProportion` is checking the pool balances to determine how much collateral should be bought/sold if the threshold is met. 
```solidity
    function getSupplyProportion() public view returns (uint256, uint256) {
        uint256 vol1 = IERC20Upgradeable(uniPool.token0()).balanceOf(
            address(uniPool)
        );
        uint256 vol2 = IERC20Upgradeable(uniPool.token1()).balanceOf(
            address(uniPool)
        );
        if (uniPool.token0() == USSD) {
            return (vol1, vol2);
        }
        return (vol2, vol1);
    }
```

POC:
1. The current pool balance is 10,000 USSD and 10,000 DAI, making it 1:1. 
2. Hacker calls a flashmint on DAI for 10,000 DAI and deposits it into the pool, making the new balance 0.5: 1.
3. Hacker calls `rebalance` forcing the protocol to make swaps to rebalance its collateral.
4. Hacker removes the 10,000 DAI from the pool and pays back the flashmint.

## Impact
The protocol would be in a constant state of disarray. Another user can call rebalance afterwards to bring the protocol back to its optimal collateralization ratio. This can be a very profitable strategy for the hacker as they know there will be a forced buyer/seller of the different collateral tokens in USSD. This can allow the attacker to sandwich the swaps conducted by the protocol as it is rebalancing as well as offer JIT.
## Code Snippet
`rebalance`: https://github.com/sherlock-audit/2023-05-USSD/blob/main/ussd-contracts/contracts/USSDRebalancer.sol#L92-L107

`getOwnValuation`: https://github.com/sherlock-audit/2023-05-USSD/blob/main/ussd-contracts/contracts/USSDRebalancer.sol#L71-L80

`getSupplyProportion`: https://github.com/sherlock-audit/2023-05-USSD/blob/main/ussd-contracts/contracts/USSDRebalancer.sol#L83-L90

## Tool used

Manual Review

## Recommendation
Use price ticks when calculating price to utilize a TWAP. This will deter the hacker as pool imbalance would be much more subtle.
