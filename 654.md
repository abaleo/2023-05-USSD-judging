Proxy

high

# Precision loss can cause protocol to sell no collateral and leaving it unable to rebalance to peg

## Summary

In contract `USSDRebalancer` precision loss in [`BuyUSSDSellCollateral()`](https://github.com/sherlock-audit/2023-05-USSD/blob/main/ussd-contracts/contracts/USSDRebalancer.sol#L109) will cause variable [`amountToSellUnits`](https://github.com/sherlock-audit/2023-05-USSD/blob/main/ussd-contracts/contracts/USSDRebalancer.sol#L121) to return 0.

## Vulnerability Detail

`amountToSellUnits` is calculated like:
```js
uint256 amountToSellUnits = IERC20Upgradeable(collateral[i].token).balanceOf(USSD) * ((amountToBuyLeftUSD * 1e18 / collateralval) / 1e18) / 1e18;
```

It does not take into account that `collateral[i].token` can be `WBTC` which uses 8 decimals. Also `((amountToBuyLeftUSD * 1e18 / collateralval) / 1e18)` will always be 0 because of excessive division by `1e18`. For better understanding here is a POC.

```js
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import "forge-std/Test.sol";

contract AmountToSellUnitsTest is Test {
    uint256 collateralTokenBalanceDecimals = 1e18; // can be 1e18 (DAI, WETH, WBGL) or 1e8 (WBTC)
    uint256 amountToBuyLeftUSDDecimals = 1e18;
    uint256 collateralvalDecimals = 1e18;

    uint256 collateralTokenBalance = 20000 * collateralTokenBalanceDecimals; // representative of `IERC20Upgradeable(collateral[i].token).balanceOf(USSD)`
    uint256 amountToBuyLeftUSD = 1000 * amountToBuyLeftUSDDecimals;
    uint256 collateralval = 20000 * collateralvalDecimals; // same as collateralTokenBalance if DAI

    function test_PrecisionLoss_AmountToSellUnits() public view {
        // MUST BE: collateralval > amountToBuyLeftUSD
        uint256 amountToSellUnits = collateralTokenBalance * ((amountToBuyLeftUSD * 1e18 / collateralval) / 1e18) / 1e18;
        console.log("amountToSellUnits:     ", amountToSellUnits); // OUTPUT: 0
        // console.log("collateralTokenBalance:", collateralTokenBalance);
        // console.log("amountToBuyLeftUSD:    ", amountToBuyLeftUSD);
        // console.log("collateralval:         ", collateralval);
        console.log("middle value:          ", ((amountToBuyLeftUSD * 1e18 / collateralval) / 1e18)); // OUTPUT: 0
    }

    function test_Corrected_AmountToSellUnits() public view {
        // MUST BE: collateralval > amountToBuyLeftUSD
        uint256 amountToSellUnits =
            collateralTokenBalance * (amountToBuyLeftUSD * 1e18 / collateralval) / collateralTokenBalanceDecimals;
        console.log("amountToSellUnits:     ", amountToSellUnits); // OUTPUT: 1000000000000000000000 or 1000 * 1e18
        // console.log("collateralTokenBalance:", collateralTokenBalance);
        // console.log("amountToBuyLeftUSD:    ", amountToBuyLeftUSD);
        // console.log("collateralval:         ", collateralval);
        console.log("middle value:          ", (amountToBuyLeftUSD * 1e18 / collateralval)); // OUTPUT: 50000000000000000 or 0.05 * 1e18
    }
}

```

## Impact

Protocol is unable to rebalance to $1 peg

## Code Snippet

[USSDRebalancer.sol#L121](https://github.com/sherlock-audit/2023-05-USSD/blob/main/ussd-contracts/contracts/USSDRebalancer.sol#L121)

## Tool used

Manual Review

## Recommendation

Change `amountToSellUnits` to:

```js
uint256 amountToSellUnits = IERC20Upgradeable(collateral[i].token).balanceOf(USSD) * (amountToBuyLeftUSD * 1e18 / collateralval) / (10**IERC20MetadataUpgradeable(collateral[i].token).decimals());
```
