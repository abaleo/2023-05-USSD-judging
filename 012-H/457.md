0xHati

medium

# BuyUSSDSellCollateral will revert when receiving more dai than expected due to underflow

## Summary
The oracle price is used to determine how much units to sell to receive dai. However there's no guarantee with regards to the output since the uniswap swap uses an exact input amount and not output amount. The amount bought gets substracted by the `amountToBuyLeftUSD` which results in an overflow and revert if output > expected.
## Vulnerability Detail
First the protocol gets the value of the collateral held by the `USSD` contract of a certain asset. Then it calculates how much units of that assets it needs to sell. Finally it sells those on uniswap and substracts the `amountToBuyLeftUSD` with the current balance `dai` and the before balance to know how much was bought in the process. The problem lies in the fact that the oracles are used to get the value of the collateral and calculate how much units to sell, but the price on uniswap will most likely not be exactly like the oracle price.

## Impact
This might seem like a minor issue, however any discrepancy where what actually got received is bigger even with 1 wei vs what was expected to be received will make it impossible to rebalance.  

## Code Snippet
[Link to code](https://github.com/sherlock-audit/2023-05-USSD/blob/main/ussd-contracts/contracts/USSDRebalancer.sol#L123)
```solidity
 for (uint256 i = 0; i < collateral.length; i++) {
        //gets the value in usd of the collateral
        uint256 collateralval = IERC20Upgradeable(collateral[i].token).balanceOf(USSD) * 1e18 / (10**IERC20MetadataUpgradeable(collateral[i].token).decimals()) * collateral[i].oracle.getPriceUSD() / 1e18; 
        if (collateralval > amountToBuyLeftUSD) {
          // sell a portion of collateral and exit
          if (collateral[i].pathsell.length > 0) {
            //gets the dai balance
            uint256 amountBefore = IERC20Upgradeable(baseAsset).balanceOf(USSD);
            //calculates the units to sell: balanceCollateral * (amountToBuyLeftUSD/collateralval)
            uint256 amountToSellUnits = IERC20Upgradeable(collateral[i].token).balanceOf(USSD) * ((amountToBuyLeftUSD * 1e18 / collateralval) / 1e18) / 1e18;
            //sell via uniswap
            IUSSD(USSD).UniV3SwapInput(collateral[i].pathsell, amountToSellUnits);
            //if dai received > amountToBuyLeftUSD this will  revert
            amountToBuyLeftUSD -= (IERC20Upgradeable(baseAsset).balanceOf(USSD) - amountBefore);
            DAItosell += (IERC20Upgradeable(baseAsset).balanceOf(USSD) - amountBefore);
          } else {
            // no need to swap DAI
            DAItosell = IERC20Upgradeable(collateral[i].token).balanceOf(USSD) * amountToBuyLeftUSD / collateralval;
          }
          break;
        }
...
}
```
[Link to code]("https://github.com/sherlock-audit/2023-05-USSD/blob/main/ussd-contracts/contracts/USSDRebalancer.sol#L136")
```solidity
else {
          // sell all or skip (if collateral is too little, 5% treshold)
          if (collateralval >= amountToBuyLeftUSD / 20) {
            uint256 amountBefore = IERC20Upgradeable(baseAsset).balanceOf(USSD);
            // sell all collateral and move to next one
            IUSSD(USSD).UniV3SwapInput(collateral[i].pathsell, IERC20Upgradeable(collateral[i].token).balanceOf(USSD));
            amountToBuyLeftUSD -= (IERC20Upgradeable(baseAsset).balanceOf(USSD) - amountBefore);
            DAItosell += (IERC20Upgradeable(baseAsset).balanceOf(USSD) - amountBefore);
          }
}
```
## Tool used

Manual Review

## Recommendation
You can check first if  `DAItosell` >= `amountToBuyLeftUSD`, if it's not you can substract, if it is you can set `amountToBuyLeftUSD` to 0.

```diff
-amountToBuyLeftUSD -= (IERC20Upgradeable(baseAsset).balanceOf(USSD) - amountBefore);
-DAItosell += (IERC20Upgradeable(baseAsset).balanceOf(USSD) - amountBefore);
+DAItosell += (IERC20Upgradeable(baseAsset).balanceOf(USSD) - amountBefore);
+if (DAItosell >= amountToBuyLeftUSD) {
+                       amountToBuyLeftUSD = 0;
+                  } else {
+                    amountToBuyLeftUSD -= (IERC20Upgradeable(baseAsset).balanceOf(USSD) - amountBefore);
+              }
```
