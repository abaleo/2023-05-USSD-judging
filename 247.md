nobody2018

medium

# USSDRebalancer.BuyUSSDSellCollateral miscalculates DAItosell in certain case

## Summary

When DAI is not the first item in the array returned by `IUSSD(USSD).collateralList()`, the value of `DAItosell` may not be the correct value. The reason why DAI may not be the first item is:

1.  When USSD was deployed, DAI was not the first collateral to be added.
2.  `USSD.swapCollateralIndexes/removeCollateral` can change the position of the collateral in the array.

## Vulnerability Detail

The code snippet of `USSDRebalancer.BuyUSSDSellCollateral` is as follows:

```solidity
function BuyUSSDSellCollateral(uint256 amountToBuy) internal {
      CollateralInfo[] memory collateral = IUSSD(USSD).collateralList();
      //uint amountToBuyLeftUSD = amountToBuy * 1e12 * 1e6 / getOwnValuation();
      uint amountToBuyLeftUSD = amountToBuy * 1e12;
      uint DAItosell = 0;
      // Sell collateral in order of collateral array
      for (uint256 i = 0; i < collateral.length; i++) {
        uint256 collateralval = IERC20Upgradeable(collateral[i].token).balanceOf(USSD) * 1e18 / (10**IERC20MetadataUpgradeable(collateral[i].token).decimals()) * collateral[i].oracle.getPriceUSD() / 1e18;
->      if (collateralval > amountToBuyLeftUSD) {
          // sell a portion of collateral and exit
          if (collateral[i].pathsell.length > 0) {
            ...
            DAItosell += (IERC20Upgradeable(baseAsset).balanceOf(USSD) - amountBefore);
          } else {
            // no need to swap DAI
->          DAItosell = IERC20Upgradeable(collateral[i].token).balanceOf(USSD) * amountToBuyLeftUSD / collateralval;
          }
          break;
        } else {
          // sell all or skip (if collateral is too little, 5% treshold)
          if (collateralval >= amountToBuyLeftUSD / 20) {
            ...
            DAItosell += (IERC20Upgradeable(baseAsset).balanceOf(USSD) - amountBefore);
          }
        }
      }
      ....
    }
```

When `if (collateralval > amountToBuyLeftUSD)` is met, if `collateral[i]` is DAI, `DAItosell = IERC20Upgradeable(collateral[i].token).balanceOf(USSD) * amountToBuyLeftUSD / collateralval;` will be executed. This means that if `i` is not 0 at this time, the previous value of `DAItosell` will not be accumulated. The code here can only work normally when the index of DAI in the array is 0, that is to say, when DAI is the first item in the array. Otherwise, `DAItosell` is a smaller value than it actually is.

## Impact

- Smaller DAItosell, less UUSD to buy.
- There is more DAI in the UUSD contract. Because the overridden DAItosell is not used.

## Code Snippet

https://github.com/sherlock-audit/2023-05-USSD/blob/main/ussd-contracts/contracts/USSDRebalancer.sol#L127

## Tool used

Manual Review

## Recommendation

```diff
--- a/ussd-contracts/contracts/USSDRebalancer.sol
+++ b/ussd-contracts/contracts/USSDRebalancer.sol
@@ -124,7 +124,7 @@ contract USSDRebalancer is AccessControlUpgradeable, IUSSDRebalancer {
             DAItosell += (IERC20Upgradeable(baseAsset).balanceOf(USSD) - amountBefore);
           } else {
             // no need to swap DAI
-            DAItosell = IERC20Upgradeable(collateral[i].token).balanceOf(USSD) * amountToBuyLeftUSD / collateralval;
+            DAItosell += IERC20Upgradeable(collateral[i].token).balanceOf(USSD) * amountToBuyLeftUSD / collateralval;
           }
           break;
         } else {
```