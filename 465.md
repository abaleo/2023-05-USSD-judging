0xyPhilic

high

# `getPriceUSD()` in `StableOracleDAI.sol` returns incorrect price

## Summary

Whenever a user invokes the `mintForToken()`  function in `USSD.sol` the amount of USSD that the user will receive is returned by the `calculateMint()` function and is based on the asset price which is returned from calling `getPriceUSD()` on the `StableOracleDAI.sol` which computes the current DAI/USD price by taking into account UniswapV3 WETH/DAI price, Chainlink ETH/USD price and Chainlink DAI/ETH price. Note that `DAI` is the only collateral that is supported for minting `USSD`.

## Vulnerability Detail

The problem arises in the [getUsdPrice()](https://github.com/sherlock-audit/2023-05-USSD/blob/main/ussd-contracts/contracts/oracles/StableOracleDAI.sol#L50-L53) which returns wrong data due to misconfiguration of the formula to calculate the price. 

This is currently how the price of DAI in USD is calculated:

```solidity
 return
            (wethPriceUSD * 1e18) /
            ((DAIWethPrice + uint256(price) * 1e10) / 2);
    }
```

where `wethPriceUSD` is obtained from Chainlink ETH/USD aggregator, `DAIWethPrice` is computed from UniswapV3 DAI/WETH pool and `price` is the answer returned by Chainlink DAI/ETH aggregator.

## Impact

A user that tries to `mintForToken()` a specific amount of USSD with DAI will actually receive way less USSD thus losing significant amount of his/hers funds. Running a test using Forge on Ethereum Mainnet Fork with 100 DAI as input yields only 0.065810 USSD for the user. Without any backward compatibility i.e. the user being able to sell back the USSD for his DAI on the protocol, the user is forced to sell the USSD on UniswapV3 DAI/USSD pool essentially leading to a significant loss.

## Code Snippet

PoC setup: 

1. In a new directory install foundryup and run `foundryup` in the terminal to install `forge`, `cast`, `anvil` etc.
2. Run `forge init some_name` which will initialize a new forge project
3. Copy `foundry.toml` in the USSD project directory
4. Create a `forge-cashe` directory under `ussd-contracts`
5. Copy `lib` from the new forge project from step 2 to the `ussd-contracts` folder
6. Create a `remappings.txt` file in the `ussd-contracts` folder
7. In `test` folder in `ussd-contracts` create 2 folders: `utils` and `foundry`

Update `remappings.txt` as follows:

```txt
ds-test/=lib/forge-std/lib/ds-test/src/
forge-std/=lib/forge-std/src/
@openzeppelin/=node_modules/@openzeppelin
@uniswap/=node_modules/@uniswap
@chainlink/=node_modules/@chainlink
```

Update `foundry.toml` as follows:

```txt
[profile.default]
src = "contracts"
out = "out"
libs = ["node_modules", "lib"]
test = "test/foundry"
cache-path = "forge-cache"
```

In `test/utils` folder create 2 contracts:

Utilities.sol with the following code:

```solidity
// SPDX-License-Identifier: MIT
pragma solidity >=0.8.0;

import "forge-std/Test.sol";

contract Utilities is Test {
    bytes32 internal nextUser = keccak256(abi.encodePacked("user address"));

    function getNextUserAddress() external returns (address payable) {
        //bytes32 to address conversion
        address payable user = payable(address(uint160(uint256(nextUser))));
        nextUser = keccak256(abi.encodePacked(nextUser));
        return user;
    }

    /// @notice create users with 100 ether balance
    function createUsers(uint256 userNum) external returns (address payable[] memory) {
        address payable[] memory users = new address payable[](userNum);
        for (uint256 i = 0; i < userNum; i++) {
            address payable user = this.getNextUserAddress();
            vm.deal(user, 100 ether);
            users[i] = user;
        }
        return users;
    }

    /// @notice move block.number forward by a given number of blocks
    function mineBlocks(uint256 numBlocks) external {
        uint256 targetBlock = block.number + numBlocks;
        vm.roll(targetBlock);
    }
}
```

SqrtMath.sol as follows:

```solidity
// SPDX-License-Identifier: GPL2.0
// https://github.com/gakonst/v3-periphery-foundry/blob/main/contracts/foundry-tests/utils/Math.sol#L6
pragma solidity ^0.8.0;

uint256 constant PRECISION = 2**96;

// Computes the sqrt of the u64x96 fixed point price given the AMM reserves
function encodePriceSqrt(uint256 reserve1, uint256 reserve0) pure returns (uint160) {
    return uint160(sqrt((reserve1 * PRECISION * PRECISION) / reserve0));
}

// Fast sqrt, taken from Solmate.
function sqrt(uint256 x) pure returns (uint256 z) {
    assembly {
        // Start off with z at 1.
        z := 1

        // Used below to help find a nearby power of 2.
        let y := x

        // Find the lowest power of 2 that is at least sqrt(x).
        if iszero(lt(y, 0x100000000000000000000000000000000)) {
            y := shr(128, y) // Like dividing by 2 ** 128.
            z := shl(64, z) // Like multiplying by 2 ** 64.
        }
        if iszero(lt(y, 0x10000000000000000)) {
            y := shr(64, y) // Like dividing by 2 ** 64.
            z := shl(32, z) // Like multiplying by 2 ** 32.
        }
        if iszero(lt(y, 0x100000000)) {
            y := shr(32, y) // Like dividing by 2 ** 32.
            z := shl(16, z) // Like multiplying by 2 ** 16.
        }
        if iszero(lt(y, 0x10000)) {
            y := shr(16, y) // Like dividing by 2 ** 16.
            z := shl(8, z) // Like multiplying by 2 ** 8.
        }
        if iszero(lt(y, 0x100)) {
            y := shr(8, y) // Like dividing by 2 ** 8.
            z := shl(4, z) // Like multiplying by 2 ** 4.
        }
        if iszero(lt(y, 0x10)) {
            y := shr(4, y) // Like dividing by 2 ** 4.
            z := shl(2, z) // Like multiplying by 2 ** 2.
        }
        if iszero(lt(y, 0x8)) {
            // Equivalent to 2 ** z.
            z := shl(1, z)
        }

        // Shifting right by 1 is like dividing by 2.
        z := shr(1, add(z, div(x, z)))
        z := shr(1, add(z, div(x, z)))
        z := shr(1, add(z, div(x, z)))
        z := shr(1, add(z, div(x, z)))
        z := shr(1, add(z, div(x, z)))
        z := shr(1, add(z, div(x, z)))
        z := shr(1, add(z, div(x, z)))

        // Compute a rounded down version of z.
        let zRoundDown := div(x, z)

        // If zRoundDown is smaller, use it.
        if lt(zRoundDown, z) {
            z := zRoundDown
        }
    }
}
```

In `test/foundry` create a `BaseSetup.t.sol` contract with the below setup code. This contract takes care of deploying all necessary contracts from the system as well as creating UniswapV3 pool, setting the proper oracles (Ethereum Mainnet) etc.

```solidity
pragma solidity ^0.8.6;

import "forge-std/Test.sol";
import "../utils/SqrtMath.sol";

import "../../contracts/USSD.sol";
import "../../contracts/USSDRebalancer.sol";
import "../../contracts/interfaces/IUSSDRebalancer.sol";
import { Utilities } from "../utils/Utilities.sol";
import "@uniswap/v3-core/contracts/interfaces/IUniswapV3Factory.sol";
import "@uniswap/v3-core/contracts/interfaces/IUniswapV3Pool.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@uniswap/swap-router-contracts/contracts/interfaces/IWETH.sol";
import "@uniswap/swap-router-contracts/contracts/interfaces/IV3SwapRouter.sol";
import { StaticOracle } from "../../contracts/oracles/UniswapV3StaticOracle.sol";
import "../../contracts/oracles/StableOracleDAI.sol";
import "../../contracts/oracles/StableOracleWBTC.sol";
import "../../contracts/oracles/StableOracleWBGL.sol";
import "../../contracts/oracles/StableOracleWETH.sol";
import "@uniswap/v3-core/contracts/libraries/TickMath.sol";
import "@uniswap/v3-periphery/contracts/interfaces/INonfungiblePositionManager.sol";
import "@uniswap/v3-periphery/contracts/interfaces/IQuoter.sol";

contract BaseSetup is Test {

    Utilities internal utils;
    USSD internal _ussd;
    USSDRebalancer internal _rebalancer;
    IUniswapV3Factory internal _uniswapFactory;
    INonfungiblePositionManager internal _nftp;
    IQuoter internal _quoter;

    IERC20 internal _dai;
    IERC20 internal _wbtc;
    IERC20 internal _wbgl;
    IWETH internal _weth;

    IV3SwapRouter internal _uniRouter;
    IUniswapV3Pool internal _uniPool;
    StableOracleWBGL internal _stableOracleWBGL;
    StableOracleWBTC internal _stableOracleWBTC;
    StableOracleWETH internal _stableOracleWETH;
    StableOracleDAI internal _stableOracleDAI;
    StaticOracle internal _uniswapStaticOracle;

    address[] internal users;

    address payable _controller;
    address payable _attacker;
    address payable _bob;
    address payable _alice;
    address payable _sam;

    
    function setUp() external {

        // All contracts are mainnet contracts - Tests should be ran on Ethereum mainnet fork

        _uniswapFactory = IUniswapV3Factory(0x1F98431c8aD98523631AE4a59f267346ea31F984);
        _nftp = INonfungiblePositionManager(0xC36442b4a4522E871399CD717aBDD847Ab11FE88);
        _dai = IERC20(0x6B175474E89094C44Da98b954EedeAC495271d0F);
        _wbtc = IERC20(0x2260FAC5E5542a773Aa44fBCfeDf7C193bc2C599);
        _wbgl = IERC20(0x2bA64EFB7A4Ec8983E22A49c81fa216AC33f383A);
        _weth = IWETH(0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2);
        _quoter = IQuoter(0xb27308f9F90D607463bb33eA1BeBb41C27CE5AB6);
        _uniRouter = IV3SwapRouter(0x68b3465833fb72A70ecDF485E0e4C7bD8665Fc45);
        _stableOracleWETH = new StableOracleWETH();
        _stableOracleWBTC = new StableOracleWBTC();
        _uniswapStaticOracle = new StaticOracle(_uniswapFactory,10);
        _stableOracleDAI = new StableOracleDAI(address(_stableOracleWETH), address(_uniswapStaticOracle));
        _stableOracleWBGL = new StableOracleWBGL(address(_stableOracleWETH), address(_uniswapStaticOracle));
        
        utils = new Utilities();

        users = utils.createUsers(5);
        _controller = payable(users[0]);
        _attacker = payable(users[1]);
        _bob = payable(users[2]);
        _alice = payable(users[3]);
        _sam = payable(users[4]);
        
        vm.startPrank(_controller);
        _ussd = new USSD();
        _rebalancer = new USSDRebalancer();
        _ussd.initialize("US Autonomous Dollar", "USSD");
        _ussd.grantRole(_ussd.STABLE_CONTROL_ROLE(), _controller);
        _ussd.setRebalancer(address(_rebalancer));
        _ussd.setUniswapRouter(address(_uniRouter));
        _weth.deposit{value: 80e18}();
        _weth.approve(address(_uniRouter), type(uint256).max);
        bytes memory path = bytes.concat(bytes20(address(_weth)), bytes3(uint24(500)), bytes20(address(_dai)));
        IV3SwapRouter.ExactInputParams memory params = IV3SwapRouter.ExactInputParams({
            path: path,
            recipient: _controller,
            amountIn: 80e18,
            amountOutMinimum: 0
        });
        _uniRouter.exactInput(params);
        _ussd.approve(address(_uniswapFactory), type(uint256).max);
        _dai.approve(address(_uniswapFactory), type(uint256).max);
        address _uniswapUSSDDAIPool = _uniswapFactory.createPool(address(_ussd),address(_dai),uint24(500));
        uint160 _initialPrice = encodePriceSqrt(10000e6, 10000e18);
        _uniPool = IUniswapV3Pool(_uniswapUSSDDAIPool);
        IUniswapV3Pool(_uniswapUSSDDAIPool).initialize(_initialPrice);
        int24 tickSpacing = IUniswapV3Pool(_uniswapUSSDDAIPool).tickSpacing();
        (, int24 curTick, , , , , ) = IUniswapV3Pool(_uniswapUSSDDAIPool).slot0();
        curTick = curTick - (curTick % tickSpacing);
        int24 lowerTick = curTick - (tickSpacing * 2);
        int24 upperTick = curTick + (tickSpacing * 2);
        _ussd.approve(address(_nftp), type(uint256).max);
        _dai.approve(address(_nftp), type(uint256).max);
        _nftp.mint(
            INonfungiblePositionManager.MintParams({
                token0: IUniswapV3Pool(_uniswapUSSDDAIPool).token0(),
                token1: IUniswapV3Pool(_uniswapUSSDDAIPool).token1(),
                fee: uint24(500),
                tickLower: lowerTick,
                tickUpper: upperTick,
                amount0Desired: 10000e18,
                amount1Desired: 10000e6,
                amount0Min: 0e18,
                amount1Min: 0e18,
                recipient: msg.sender,
                deadline: block.timestamp + 3600
            })
        );
        _rebalancer.initialize(address(_ussd));
        _rebalancer.grantRole(_rebalancer.STABLE_CONTROL_ROLE(), _controller);
        _rebalancer.setPoolAddress(_uniswapUSSDDAIPool);
        uint256[] memory ratios = new uint256[](4);
        ratios[0] = 14250000000000000000;
        ratios[1] = 28350000000000000000;
        ratios[2] = 61000000000000000000;
        ratios[3] = 112800000000000000000;
        _rebalancer.setFlutterRatios(ratios);
        _rebalancer.setBaseAsset(address(_dai));
        uint256[] memory _daiRatios = new uint256[](4);
        _daiRatios[0] = 250000000000000000;
        _daiRatios[1] = 350000000000000000;
        _daiRatios[2] = 1000000000000000000;
        _daiRatios[3] = 800000000000000000;
        _ussd.addCollateral(address(_dai), address(_stableOracleDAI), true, true, _daiRatios, "0x", "0x", 100);
        uint256[] memory _wethRatios = new uint256[](4);
        _wethRatios[0] = 2000000000000000000;
        _wethRatios[1] = 4000000000000000000;
        _wethRatios[2] = 5000000000000000000;
        _wethRatios[3] = 6000000000000000000;
        bytes memory _wethBuyPath = bytes.concat(bytes20(address(_dai)), bytes3(uint24(500)), bytes20(address(_weth)));
        bytes memory _wethSellPath = bytes.concat(bytes20(address(_weth)), bytes3(uint24(500)), bytes20(address(_dai)));
        _ussd.addCollateral(address(_weth), address(_stableOracleWETH), false, false, _wethRatios, _wethBuyPath, _wethSellPath, 100);
        bytes memory _wbtcBuyPath = bytes.concat(bytes20(address(_dai)), bytes3(uint24(500)), bytes20(address(_weth)), bytes3(uint24(500)), bytes20(address(_wbtc)));
        bytes memory _wbtcSellPath = bytes.concat(bytes20(address(_wbtc)), bytes3(uint24(500)), bytes20(address(_weth)), bytes3(uint24(500)), bytes20(address(_dai)));
        _ussd.addCollateral(address(_wbtc), address(_stableOracleWBTC), false, false, _wethRatios, _wbtcBuyPath, _wbtcSellPath, 100);
        uint256[] memory _wbglRatios = new uint256[](4);
        _wbglRatios[0] = 10000000000000000000;
        _wbglRatios[1] = 20000000000000000000;
        _wbglRatios[2] = 50000000000000000000;
        _wbglRatios[3] = 100000000000000000000;
        bytes memory _wbglBuyPath = bytes.concat(bytes20(address(_dai)), bytes3(uint24(500)), bytes20(address(_weth)), bytes3(uint24(500)), bytes20(address(_wbgl)));
        bytes memory _wbglSellPath = bytes.concat(bytes20(address(_wbgl)), bytes3(uint24(500)), bytes20(address(_weth)), bytes3(uint24(500)), bytes20(address(_dai)));
        _ussd.addCollateral(address(_wbgl), address(_stableOracleWBGL), false, false, _wbglRatios, _wbglBuyPath, _wbglSellPath, 100);
        _dai.transfer(_attacker, 50000e18);
        _dai.transfer(_alice, 50000e18);
        vm.stopPrank();
    }
}
```

Finally create a `FaultyDAIValue.t.sol` contract where we'll run our test as follows:

```solidity
pragma solidity ^0.8.6;

import "./BaseSetup.t.sol";
contract FaultyDAIValue is BaseSetup {

    function test_calculateFaultyMintValue() external {
        uint256 _daiAmount = 100e18;
        vm.startPrank(_alice);
        _dai.approve(address(_ussd), type(uint256).max);
        _ussd.mintForToken(address(_dai), _daiAmount, _alice);
        console.log("Actual balance after mint: ", _ussd.balanceOf(_alice));
        assertGt(_ussd.balanceOf(_alice), 99e6);
        vm.stopPrank();
    }
}
```

Make sure to go to `node_modules/@uniswap/swap-router-contracts/contracts/interfaces/IWETH.sol` and add the following two lines, otherwise any approve calls necessary for WETH will fail in the testing contracts:

```solidity
    function approve(address spender, uint256 amount) external returns(bool);

    function balanceOf(address account) external view returns(uint256);
```

Finally you can run the test by first setting ETH_RPC_URL="rpc_url" in the terminal and then running `forge test --fork-url $ETH_RPC_URL --match-test calculateFaultyMintValue -vv`

Below you can find the logs from that test:

```txt
[FAIL. Reason: Assertion failed.] test_calculateFaultyMintValue() (gas: 245465)
Logs:
  Actual balance after mint:  65810
  Error: a > b not satisfied [uint]
    Value a: 65810
    Value b: 99000000

Test result: FAILED. 0 passed; 1 failed; finished in 22.82s
```

## Tool used

Manual Review / Foundry

## Recommendation

Obtaining the DAI/USD price within the `getPriceUSD()` function of `StableOracleDAI.sol` can be done by simply multiplying `DAI/WETH` price and `WETH/USD`.  

Remove the DAI/ETH price request from Chainlink

```solidity
(, int256 price, , , ) = priceFeedDAIETH.latestRoundData();
```

Convert the `wethPriceUSD` to `usdWeth` by doing:

```solidity
uint256 usdWethPrice = 10 ** 36 / wethPriceUSD;
```

Now simply return: 

```solidity
return  (DAIWethPrice * usdWethPrice) / 10 ** 18;
```

Running the above test after the updates to the `getPriceUSD()` logs the following output:

```txt
[PASS] test_calculateFaultyMintValue() (gas: 213697)
Logs:
  Actual balance after mint:  99818851

Test result: ok. 1 passed; 0 failed; finished in 16.76s
```

