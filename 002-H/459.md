dacian

medium

# Precision loss to token mint amount in USSDRebalancer.rebalance()

## Summary
Precision loss to token mint amount in USSDRebalancer.rebalance().

## Vulnerability Detail
Examine [USSDRebalancer.rebalance() L104](https://github.com/sherlock-audit/2023-05-USSD/blob/main/ussd-contracts/contracts/USSDRebalancer.sol#L104):
```solidity
IUSSD(USSD).mintRebalancer(((DAIamount / 1e12 - USSDamount)/2) * 99 / 100);
```
Here ``/2 * 99/100`` results in precision loss due to [Division Before Multiplication](https://dacian.me/precision-loss-errors#heading-division-before-multiplication).

## Impact
Incorrect amount of tokens will be minted.

## Code Snippet
I have created a stand-alone test case using Foundry to show the precision loss. Contract functions:
```solidity
function ussdErrorRebalance(uint ussdAmount, uint daiAmount) 
	public view returns (uint256) {
	console.log("ussdErrorRebalance");
	console.log("ussdAmount : ", ussdAmount);
	console.log("daiAmount  : ", daiAmount);

	// @audit division before multiplication /2 * 99/100
	uint result = (((daiAmount / 1e12 - ussdAmount)/2) * 99 / 100);

	console.log("result     : ", result);
	return result;
}

function ussdCorrectRebalance(uint ussdAmount, uint daiAmount) public view returns (uint256) {
	console.log("ussdCorrectRebalance");
	console.log("ussdAmount : ", ussdAmount);
	console.log("daiAmount  : ", daiAmount);

	// @audit /2 * 99/100 same as * 99/200, fixed precision loss
        // by removing division before multiplication
	uint result = (((daiAmount / 1e12 - ussdAmount)) * 99 / 200);

	console.log("result     : ", result);
	return result;
}
```

And the test function:
```solidity
    function testUssdRebalance() public {
        console.log("case #1: everything works out, no precision loss");      
        uint uusdAmount = 15379_123456;             // 6 decimals 
        uint daiAmount  = 23254_123456789123456789; // 18 decimals

        uint errorOutput   = vulnContract.ussdErrorRebalance(uusdAmount, daiAmount);
        uint correctOutput = vulnContract.ussdCorrectRebalance(uusdAmount, daiAmount);

        assertEq(errorOutput, correctOutput);

        console.log("case #2: slightly modify input, now precision loss visible");    
        uusdAmount = 15379_123451;

        errorOutput   = vulnContract.ussdErrorRebalance(uusdAmount, daiAmount);
        correctOutput = vulnContract.ussdCorrectRebalance(uusdAmount, daiAmount);

        assert(errorOutput != correctOutput);
    }
```

Running gives the output:
```solidity
case #1: everything works out, no precision loss
ussdErrorRebalance
ussdAmount :  15379123456
daiAmount  :  23254123456789123456789
result     :  3898125000
ussdCorrectRebalance
ussdAmount :  15379123456
daiAmount  :  23254123456789123456789
result     :  3898125000
case #2: slightly modify input, now precision loss visible
ussdErrorRebalance
ussdAmount :  15379123451
daiAmount  :  23254123456789123456789
result     :  3898125001 // @audit note precision loss here
ussdCorrectRebalance
ussdAmount :  15379123451
daiAmount  :  23254123456789123456789
result     :  3898125002 // @audit actual value without precision loss

```
## Tool used
Manual Review

## Recommendation
``/2 * 99/100`` same as ``* 99/200`` but this removes the division before multiplication & hence the precision loss; see ussdCorrectRebalance() in the foundry contract methods above. 
