dacian

high

# Precision loss to amountToBuyLeftUSD, amountToSellUnits in USSDRebalancer.BuyUSSDSellCollateral()

## Summary
Precision loss to ``amountToBuyLeftUSD`` & ``amountToSellUnits`` in USSDRebalancer.BuyUSSDSellCollateral().

## Vulnerability Detail
First examine [USSDRebalancer.rebalance() L97](https://github.com/sherlock-audit/2023-05-USSD/blob/main/ussd-contracts/contracts/USSDRebalancer.sol#L97):
```solidity
BuyUSSDSellCollateral((USSDamount - DAIamount / 1e12)/2);
```
In this function call to BuyUSSDSellCollateral(), a final division by 2 is performed.

Next examine [USSDRebalancer.BuyUSSDSellCollateral()](https://github.com/sherlock-audit/2023-05-USSD/blob/main/ussd-contracts/contracts/USSDRebalancer.sol#L109-L112):
```solidity
function BuyUSSDSellCollateral(uint256 amountToBuy) internal {
  CollateralInfo[] memory collateral = IUSSD(USSD).collateralList();
  //uint amountToBuyLeftUSD = amountToBuy * 1e12 * 1e6 / getOwnValuation();
  uint amountToBuyLeftUSD = amountToBuy * 1e12;
```
Here ``amountToBuy`` which was previously divided by 2 is now multiplied again and the result stored in ``amountToBuyLeftUSD``. This results in precision loss due to [Division Before Multiplication](https://dacian.me/precision-loss-errors#heading-division-before-multiplication). The same thing also occurs at [L148-149](https://github.com/sherlock-audit/2023-05-USSD/blob/main/ussd-contracts/contracts/USSDRebalancer.sol#L148-L149):
```solidity
if (DAItosell > amountToBuy * 1e12 * 99 / 100) {
  DAItosell = amountToBuy * 1e12 * 99 / 100;
```

Next examine [L121](https://github.com/sherlock-audit/2023-05-USSD/blob/main/ussd-contracts/contracts/USSDRebalancer.sol#L121):
```solidity
uint256 amountToSellUnits = IERC20Upgradeable(collateral[i].token).balanceOf(USSD) * ((amountToBuyLeftUSD * 1e18 / collateralval) / 1e18) / 1e18;
```
Here ``amountToBuyLeftUSD`` that has already been divided, is now multiplied then divided again, resulting in more precision loss.

## Impact
Incorrect amounts will be bought & sold.

## Code Snippet
I have created a stand-alone test case to show the first precision loss using Foundry. Contract functions:
```solidity
function ussdErrorAmountToBuy(uint ussdAmount, uint daiAmount) 
	public view returns (uint256) {
	console.log("ussdErrorAmountToBuy");
	console.log("ussdAmount : ", ussdAmount);
	console.log("daiAmount  : ", daiAmount);

	// @audit /2 * 1e12 division before multiplication
	// causes precision loss
	uint result = (ussdAmount - daiAmount / 1e12)/2 * 1e12;

	console.log("result     : ", result);
	return result;
}

function ussdCorrectAmountToBuy(uint ussdAmount, uint daiAmount) public view returns (uint256) {
	console.log("ussdCorrectRebalance");
	console.log("ussdAmount : ", ussdAmount);
	console.log("daiAmount  : ", daiAmount);

	// @audit /2 * 1e12 can be rewritten as * 1e12 / 2,
	// removes division before multiplication, solving precision loss
	uint result = (ussdAmount - daiAmount / 1e12) * 1e12 / 2;

	console.log("result     : ", result);
	return result;
}
```
And the test function:
```solidity
function testUssdAmountToBuy() public {
	console.log("case #1: everything works out, no precision loss");      
	uint uusdAmount = 23254_123456;             // 6 decimals 
	uint daiAmount  = 15379_123456789123456789; // 18 decimals 

	uint errorOutput   = vulnContract.ussdErrorAmountToBuy(uusdAmount, daiAmount);
	uint correctOutput = vulnContract.ussdCorrectAmountToBuy(uusdAmount, daiAmount);

	assertEq(errorOutput, correctOutput);

	console.log("case #2: slightly modify input, now precision loss visible");    
	uusdAmount = 23254_123451;

	errorOutput   = vulnContract.ussdErrorAmountToBuy(uusdAmount, daiAmount);
	correctOutput = vulnContract.ussdCorrectAmountToBuy(uusdAmount, daiAmount);

	assert(errorOutput != correctOutput);
}
```
Running gives the output:
```solidity
case #1: everything works out, no precision loss
ussdErrorAmountToBuy
ussdAmount :  23254123456
daiAmount  :  15379123456789123456789
result     :  3937500000000000000000
ussdCorrectRebalance
ussdAmount :  23254123456
daiAmount  :  15379123456789123456789
result     :  3937500000000000000000
case #2: slightly modify input, now precision loss visible
ussdErrorAmountToBuy
ussdAmount :  23254123451
daiAmount  :  15379123456789123456789
result     :  3937499997000000000000 // @audit note precision loss here
ussdCorrectRebalance
ussdAmount :  23254123451
daiAmount  :  15379123456789123456789
result     :  3937499997500000000000 // @audit actual value without precision loss
```

## Tool used
Manual Review

## Recommendation
``/2 * 1e12`` can be rewritten as ``* 1e12 / 2``, removing division before multiplication & solving precision loss.

``((amountToBuyLeftUSD * 1e18 / collateralval) / 1e18)`` can be rewritten as ``(amountToBuyLeftUSD / collateralval)`` as multiplying and dividing by 1e18 cancels out, this removes the multiplication after division and solves the precision loss.