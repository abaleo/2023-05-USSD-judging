Dug

medium

# USDD total supply can be inflated, resulting in incorrect collateral structure

## Summary

The protocol developers put a lot of time and energy into the design of the collateral structure. A malicious actor could undermine this by inflating the total supply of USSD, causing underlying collateral to be missallocated.

## Vulnerability Detail

In the `SellUSSDBuyCollateral` function, the collateral factor of the USSD token is used to determine the `flutter` value

```solidity
    uint256 cf = IUSSD(USSD).collateralFactor();
    uint256 flutter = 0;
    for (flutter = 0; flutter < flutterRatios.length; flutter++) {
        if (cf < flutterRatios[flutter]) {
            break;
        }
    }
```

The collateral factor is determined by the following function in the `USSD` contract...

```solidity
    function collateralFactor() public view override returns (uint256) {
        uint256 totalAssetsUSD = 0;
        for (uint256 i = 0; i < collateral.length; i++) {
            totalAssetsUSD += (
                (
                    (IERC20Upgradeable(collateral[i].token).balanceOf(address(this)) * 1e18)
                        / (10 ** IERC20MetadataUpgradeable(collateral[i].token).decimals())
                ) * collateral[i].oracle.getPriceUSD()
            ) / 1e18;
        }

        return (totalAssetsUSD * 1e6) / totalSupply();
    }
```

Also in `SellUSSDBuyCollateral` the value of USSD in circulation is calculated in the following line...

```solidity
    uint256 ownval = (getOwnValuation() * 1e18 / 1e6) * IUSSD(USSD).totalSupply() / 1e6; // 1e18 total USSD value
```

Both the `flutter` value and `ownval` are used to determine how collateral is distributed across the different collateral types.

The issue is that a malicious actor can call the `mintRebalancer` function in the `USSD` contract to mint additional supply that is held within the USSD token contract. This will cause the collateral factor to be smaller and the `ownval` to be inflated, resulting in the missalocation of collateral when the `SellUSSDBuyCollateral` function is executed.

```solidity
    function mintRebalancer(uint256 amount) public override {
        _mint(address(this), amount);
    }
```

## Impact

By causing backing collateral to be missallocated, the malicious actor can compromise the integrity of the protocol by undermining decisions made by the protocol developers.

## Code Snippet

https://github.com/sherlock-audit/2023-05-USSD/blob/main/ussd-contracts/contracts/USSDRebalancer.sol#L163-L205

https://github.com/sherlock-audit/2023-05-USSD/blob/main/ussd-contracts/contracts/USSD.sol#L204-L206

## Tool used

Manual Review

## Recommendation

Add the `onlyBalancer` modifier to the `mintRebalancer` function in the `USSD` contract.
