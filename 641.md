Juntao

high

# Incorrect calculation of DAI/USD price

## Summary
Incorrect calculation of DAI/USD price.

## Vulnerability Detail
In StableOracleDAI.sol, [getPriceUSD()](https://github.com/sherlock-audit/2023-05-USSD/blob/main/ussd-contracts/contracts/oracles/StableOracleDAI.sol#L33C14-L53) is used to calculate DAI/USD price:
```solidity
    function getPriceUSD() external view override returns (uint256) {
        address[] memory pools = new address[](1);
        pools[0] = 0x60594a405d53811d3BC4766596EFD80fd545A270;
        uint256 DAIWethPrice = DAIEthOracle.quoteSpecificPoolsWithTimePeriod(
            1000000000000000000, // 1 Eth
            0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2, // WETH (base token)
            0x6B175474E89094C44Da98b954EedeAC495271d0F, // DAI (quote token)
            pools, // DAI/WETH pool uni v3
            600 // period
        );

        uint256 wethPriceUSD = ethOracle.getPriceUSD();

        // chainlink price data is 8 decimals for WETH/USD, so multiply by 10 decimals to get 18 decimal fractional
        //(uint80 roundID, int256 price, uint256 startedAt, uint256 timeStamp, uint80 answeredInRound) = priceFeedDAIETH.latestRoundData();
        (, int256 price, , , ) = priceFeedDAIETH.latestRoundData();

        return
            (wethPriceUSD * 1e18) /
            ((DAIWethPrice + uint256(price) * 1e10) / 2);
    }
```
Let's examine each variable used in the calculation:
```solidity
(wethPriceUSD * 1e18) / ((DAIWethPrice + uint256(price) * 1e10) / 2);
```

- [wethPriceUSD](https://github.com/sherlock-audit/2023-05-USSD/blob/main/ussd-contracts/contracts/oracles/StableOracleDAI.sol#L44) is ETH/USD price in 18 decimals get from [StableOracleWETH](https://github.com/sherlock-audit/2023-05-USSD/blob/main/ussd-contracts/contracts/oracles/StableOracleWETH.sol), which retrieves price data from Chainlink Price Feeds [0x5f4eC3Df9cbd43714FE2740f5E3616155c5b8419](https://etherscan.io/address/0x5f4eC3Df9cbd43714FE2740f5E3616155c5b8419):
```solidity
        uint256 wethPriceUSD = ethOracle.getPriceUSD();
``` 

- [DAIWethPrice](https://github.com/sherlock-audit/2023-05-USSD/blob/main/ussd-contracts/contracts/oracles/StableOracleDAI.sol#L36-L42) is ETH/DAI quote price in 18 decimals get from Uniswap Pool:
```solidity
        uint256 DAIWethPrice = DAIEthOracle.quoteSpecificPoolsWithTimePeriod(
            1000000000000000000, // 1 Eth
            0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2, // WETH (base token)
            0x6B175474E89094C44Da98b954EedeAC495271d0F, // DAI (quote token)
            pools, // DAI/WETH pool uni v3
            600 // period
        );
```

- [price](https://github.com/sherlock-audit/2023-05-USSD/blob/main/ussd-contracts/contracts/oracles/StableOracleDAI.sol#L48) is DAI / ETH price in 18 decimals retrieved from Chainlink Price Feeds [0x773616E4d11A78F511299002da57A0a94577F1f4](https://etherscan.io/address/0x773616E4d11A78F511299002da57A0a94577F1f4):
```solidity
        (, int256 price, , , ) = priceFeedDAIETH.latestRoundData();
```

We can then see the way of calculation is essentially `ETH/USD / ((ETH/DAI + DAI/ETH) / 2)` which makes no sense.

## Impact
Incorrect DAI/USD price.

## Code Snippet
https://github.com/sherlock-audit/2023-05-USSD/blob/main/ussd-contracts/contracts/oracles/StableOracleDAI.sol#L33C14-L53

## Tool used

Manual Review

## Recommendation
The correct way of calculation should be like (ETH/USD) * ((DAI/ETH + DAI/ETH) / 2) , please consider to modify as the followings:

1. get DAI/ETH price instead of ETH/DAI from Uniswap Pool:

```solidity
        // returns quoteAmount Amount of quoteToken received for baseAmount of baseToken
        uint256 DAIWethPrice = DAIEthOracle.quoteSpecificPoolsWithTimePeriod(
            1000000000000000000, // 1 DAI
            0x6B175474E89094C44Da98b954EedeAC495271d0F, // DAI (base token)
            0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2, // WETH (quote token)
            pools, // DAI/WETH pool uni v3
            600 // period
        );
```
2. Change the way of calculation:
```solidity
(wethPriceUSD) * ((DAIWethPrice + uint256(price)) / 2) / 1e18;
```