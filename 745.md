0xyPhilic

medium

# A malicious user with `STABLE_CONTROL_ROLE` can drain all funds

## Summary

A user with the `STABLE_CONTROL_ROLE` can update major protocol dependencies. One of those is setting the `uniRouter` address in `USSD.sol` contract. 

## Vulnerability Detail

The vulnerability relies on an actor with the `STABLE_CONTROL_ROLE` who is malicious. The actor can re-set the `uniRouter` address to point to his own address or a malicious smart contract by invoking the `setUniswapRouter()` function inside `USSD.sol`. Once this is done, the malicious actor can call the `approveToRouter()` which is public and approves the `uniRouter` to spend any token inside of `USSD.sol` - as example DAI. Then the attacker simply has to call `transferFrom()` passing the address of the USSD.sol contract, his address as recipient and the balance of the contract for the specific token, which will transfer all funds from the contract to the attacker address.

## Impact

Complete loss of user deposited funds as well as funds obtained by the protocol during `rebalance()` procedures.

## Code Snippet

The following test shows that the initial DAI balance of the contract is 3000 after `Bob` mints USSD and after the operations performed by the malicious actor the DAI balance of the contract is 0.

```solidity
function test_stableControlExploit() external {
        // Bob mints some USSD with DAI using mintForToken()
        vm.startPrank(_bob);
        _dai.approve(address(_ussd), type(uint256).max);
        _ussd.mintForToken(address(_dai), 3000e18, _bob);
        vm.stopPrank();
        // Malicious owner gives stable control role to attacker
        vm.startPrank(_controller);
        _ussd.grantRole(_ussd.STABLE_CONTROL_ROLE(), address(_attacker));
        vm.stopPrank();
        // Malicious STABLE_CONTROL_ROLE holder re-sets the uniRouter address, approves it for DAI and transfers all dai from USSD.sol to him
        vm.startPrank(_attacker);
        _ussd.setUniswapRouter(_attacker);
        _ussd.approveToRouter(address(_dai));
        _dai.transferFrom(address(_ussd), _attacker, _dai.balanceOf(address(_ussd)));
        assertEq(_dai.balanceOf(address(_ussd)), 0);
    }
```

## Tool used

Manual Review / Foundry

## Recommendation

Remove the `approveToRouter()` function and instead use `approve()` within the `UniV3SwapInput` function before the actual swap on Uniswap is executed by passing `uniRouter` as spender and `_sellAmount` as value:

```solidity
function UniV3SwapInput(
        bytes memory _path,
        uint256 _sellAmount,
        address _token
    ) public onlyBalancer {
        IERC20Upgradeable(_token).approve(address(uniRouter), _sellAmount);
        IV3SwapRouter.ExactInputParams memory params = IV3SwapRouter
            .ExactInputParams({
                path: _path,
                recipient: address(this),
                //deadline: block.timestamp,
                amountIn: _sellAmount,
                amountOutMinimum: 0
            });
        uniRouter.exactInput(params);
    }
```
The operation adds a bit of gas, but ensures that collateral can't be stolen by malicious actor or lost/stolen private key. It also ensures that the router never has more `allowance` than what it needs to execute the swap.

**Make sure to update the calls to the `UniV3SwapInput` function from `USSDRebalancer.sol` to pass also as an argument the token address:**

- https://github.com/sherlock-audit/2023-05-USSD/blob/main/ussd-contracts/contracts/USSDRebalancer.sol#L122
- https://github.com/sherlock-audit/2023-05-USSD/blob/main/ussd-contracts/contracts/USSDRebalancer.sol#L135
- https://github.com/sherlock-audit/2023-05-USSD/blob/main/ussd-contracts/contracts/USSDRebalancer.sol#L154
- https://github.com/sherlock-audit/2023-05-USSD/blob/main/ussd-contracts/contracts/USSDRebalancer.sol#L156
- https://github.com/sherlock-audit/2023-05-USSD/blob/main/ussd-contracts/contracts/USSDRebalancer.sol#L169
- https://github.com/sherlock-audit/2023-05-USSD/blob/main/ussd-contracts/contracts/USSDRebalancer.sol#L173
- https://github.com/sherlock-audit/2023-05-USSD/blob/main/ussd-contracts/contracts/USSDRebalancer.sol#L201
