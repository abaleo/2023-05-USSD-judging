WATCHPUG

high

# UniV3 Router's `ExactInputParams` must have a deadline set. Leaving it at the default value of 0 will result in a revert.

## Summary

## Vulnerability Detail

The current implementation of `UniV3SwapInput()` has the deadline of the `ExactInputParams` commented out, which will leave it to its default value of 0.

However, Uniswap v3's SwapRouter does not allow the deadline to be set as 0.

Particularly, the call will revert with the error: "Transaction too old" in `checkDeadline`.

https://github.com/Uniswap/v3-periphery/blob/main/contracts/SwapRouter.sol#L132

```solidity
function exactInput(ExactInputParams memory params)
    external
    payable
    override
    checkDeadline(params.deadline)
    returns (uint256 amountOut)
{
    address payer = msg.sender; // msg.sender pays for the first hop

    while (true) {
        bool hasMultiplePools = params.path.hasMultiplePools();

        // the outputs of prior swaps become the inputs to subsequent ones
        params.amountIn = exactInputInternal(
            params.amountIn,
            hasMultiplePools ? address(this) : params.recipient, // for intermediate swaps, this contract custodies
            0,
            SwapCallbackData({
                path: params.path.getFirstPool(), // only the first pool in the path is necessary
                payer: payer
            })
        );

        // decide whether to continue or terminate
        if (hasMultiplePools) {
            payer = address(this); // at this point, the caller has paid
            params.path = params.path.skipToken();
        } else {
            amountOut = params.amountIn;
            break;
        }
    }

    require(amountOut >= params.amountOutMinimum, 'Too little received');
}
```

https://github.com/Uniswap/v3-periphery/blob/main/contracts/base/PeripheryValidation.sol

```solidity
modifier checkDeadline(uint256 deadline) {
    require(_blockTimestamp() <= deadline, 'Transaction too old');
    _;
}
```

## Impact

The `rebalancer` which utilizes UniV3SwapInput will always revert.

## Code Snippet

https://github.com/sherlock-audit/2023-05-USSD/blob/main/ussd-contracts/contracts/USSD.sol#L227-L240

## Tool used

Manual Review

## Recommendation

Uncomment "deadline: block.timestamp,".